/* shot_busy.stt .. use LED:ACT to indicate busy (AMBER), then leave it at Green */


program wrClkMon
option +r;

int tune_amber; assign tune_amber to "{uut}:SYS:LED:CLK_R";

int pps_active; assign pps_active to "{uut}:0:WR:PPS:ACTIVE"; 		monitor pps_active;
int tune; 	 	assign tune to 	"{uut}:11:Si5326:TUNEPHASE:BUSY"; 	monitor tune;
int tune_ok; 	assign tune_ok to  "{uut}:11:Si5326:TUNEPHASE:OK"; 	monitor tune_ok;

int pps_los;	assign pps_los to "{uut}:0:WR:PPS:LOS";
int force_training; assign force_training to "";
int verbose;

int pps_active_missing_seconds;
int has_480;

ss wrClkMon {
	state init {
		when() {
			if (macValueGet("verbose")){
				verbose = macValueGet("verbose")[0] - '0';
			}
			if (macValueGet("has_480")){
				has_480 = macValueGet("has_480")[0] - '0';
				if (has_480){
					pvAssign(force_training, "{uut}:0:SIG:CLK:TRAIN_REQ");	
				}
			}
			if (verbose) printf("init=>wait_pps\n");
		} state wait_pps
	}	
	state wait_pps {
		when(pps_active){
			if (verbose) printf("wait_pps=>wait_tuning\n");

			tune_amber = 1; pvPut(tune_amber);	// tuning required
			// DO NOT clear pps_los here : TUNEPHASE:OK will simply pop back up ..
		} state wait_tuning
	}
	
	state wait_tuning {
		when(tune){
			if (verbose) printf("wait_tuning=>tuning\n");
			tune_amber = !tune_amber; pvPut(tune_amber);
		} state tuning
		
		when(!pps_active){			
			if (verbose) printf("wait_tuning=>wait_pps\n");
		} state wait_pps		
	}
	
	state tuning {
		entry {
			pps_los = 0; pvPut(pps_los);	// TUNEPHASE:OK:r down, safe to clear pps_los
			if (has_480){
				force_training = 1; pvPut(force_training);
			}
		}

		when(pps_los){			
			if (verbose) printf("tune_ok=>wait_pps\n");
		} state wait_pps

		when(!tune){
			if (verbose) printf("tuning=>tuning99\n");
		} state tuning99
		
		when(delay(1)){
			if (verbose > 1) printf("tuneBusy toggle\n");
			tune_amber = !tune_amber; pvPut(tune_amber);
		} state tuning
	}
	state tuning99 {
		when(tune){
			if (verbose) printf("tuning99=>tuning\n");
		} state tuning
		
		when(pps_los){
			if (verbose) printf("tuning99=>wait_pps\n");
		} state wait_pps
		
		when(tune_ok){
			if (verbose) printf("tuning99=>tune_ok\n");
			tune_amber = 0; pvPut(tune_amber);
		} state tune_ok_state
	}

	state tune_ok_state {
		when(pps_los){
			tune_amber = 1; pvPut(tune_amber);			// RED: FAIL
			if (verbose) printf("tune_ok=>wait_pps\n");
		} state wait_pps

		when(tune){
			if (verbose) printf("tune_ok=>tuning\n");
			tune_amber = 1; pvPut(tune_amber);
		} state tuning
		
		when (!tune_ok){
			if (verbose) printf("tune_ok=>wait_tuning\n");
			tune_amber = 1; pvPut(tune_amber);
		} state wait_tuning
	}
}

ss wrPpsLos {
	state wait_pps_good {
		when(pps_active){
			pps_active_missing_seconds = 0;
			if (verbose) printf("wait_pps_good=>wait_pps_bad\n");
		}  state wait_pps_bad
	}

	state wait_bad_delay {
		when(delay(2)){
			
		} state wait_pps_bad
	} 
	state wait_pps_bad {
		when(!pps_active){
			if (verbose) printf("wait_pps_bad=>check_if_los %d %d\n", pps_active, pps_active_missing_seconds);
			pps_active_missing_seconds += 1;
		} state check_if_los
		
		when(pps_active){
			pps_active_missing_seconds = 0;
			if (verbose) printf("wait_pps_bad=>wait_bad_delay %d %d\n", pps_active, pps_active_missing_seconds);
		} state wait_bad_delay
	}
	
	state check_if_los {
		when(!pps_active && pps_active_missing_seconds > 1) {
			pps_los = 1; pvPut(pps_los);
			if (verbose) printf("check_if_los=>wait_pps_good %d %d LOS\n", pps_active, pps_active_missing_seconds);
		} state wait_pps_good
		
		when(delay(2)){
			
		} state wait_pps_bad
	}
}

