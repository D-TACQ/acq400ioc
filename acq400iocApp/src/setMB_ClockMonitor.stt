/* setting MB clock doesn't always work. 
 * So detect change requests and make sure. Also detect loss of clock and act if requested
 *
 * macros: uut, retry=1 : RETRY if recurrent fail detected, else simply notify
 */

program setMB_ClockMonitor
option +r;

/* INPUTS */
double Fs;	assign Fs to "{uut}:0:SIG:CLK_MB:SET";
double Fm; 	assign Fm to "{uut}:0:SIG:CLK_MB:FREQ";
/* OUTPUTS */
int is_ready;	assign is_ready to "{uut}:0:SIG:CLK_MB:READY";

/* Algorithm:
 *
 * ON Fs Change: 
 *    set CLK_MB:READY=FALSE
 *    on Fm == Fs:
 *        set CLK_MB:READY
 * ON Fm Change:
 *    on fourth time of asking, trigger a retry if retry=2
 *    reports:
 *    "FAILED TO TAKE, RETRY" : forcing a retry because a setting didn't stick
 *    "LOST, RETRY"           : forcing a retry because clock FAILED (retry=1)
 *    "LOST, notice only"     : notify loss of clock, no retry (retry=0)
 */
 
char* retry = "0";

monitor Fs;
monitor Fm;

double Fs0;
double Fm0;

double TH = 0.05;

%{
double fabs(double x);
/*#include <math.h>*/
static int in_bounds(double x1, double x2, double th)
{
	if (x2 == 0){
		return 0;
	}
	return fabs(x1-x2)/x2 < th; 
}	
}%

int pollcat;

ss rate_set 
{
	state init {
		when() {
			retry = macValueGet("retry");
			printf("setMB_ClockMonitor th:%.2f retry %s\n", TH, retry);
			pvGet(Fs);
			Fs0 = Fs;
			is_ready = 0; pvPut(is_ready);
		} state wait_actual	
	} 
		
	state wait_actual {
		when (delay(1)){
			;
		} state monitor_actual
	}
	
	state monitor_actual {
		when(Fs > 0 && Fs != Fs0 ){
			Fs0 = Fs;
			pollcat = 0;
			is_ready = 0; pvPut(is_ready);
		} state wait_actual
			
		when(Fs > 0 && Fm != Fm0 && in_bounds(Fm, Fs, TH)){
			Fm0 = Fm;
			is_ready = 1; pvPut(is_ready);
		} state wait_actual 
		
		when(Fs > 0 && Fm != Fm0 && !in_bounds(Fm, Fs, TH)){
			if (++pollcat > 4){				
				Fm0 = Fm;
				printf("WARNING: MB CLK Fs:%.2f != Fm:%.2f,%.2f %s\n",
					Fs, Fm, Fm0, !is_ready? "FAILED TO TAKE, RETRY": retry[0] == '1'? "LOST, RETRY":"LOST, notice only");
					
				if (!is_ready || retry[0] == '1'){
					pvPut(Fs);
				}
				is_ready = 0; pvPut(is_ready);
				pollcat = 0;
			}
		} state wait_actual
	}
}
