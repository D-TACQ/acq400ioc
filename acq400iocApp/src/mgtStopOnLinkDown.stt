/*
mgtStopOnLinkDown.st : stop streaming if link goes down
*/

program mgtStopOnLinkDown
option +r;


int aggX;
assign aggX to "{uut}:{port}:AGGREGATOR";
monitor aggX;

int linkX_up;
assign linkX_up to "{uut}:{port}:AURORA:UP";
monitor linkX_up;

int linkX_up0;

char* port;

%%#include <stdlib.h>
%{
static void _stop_stream()
{
	/* maybe caput {uut}:MODE:CONTINUOUS 0 */
	system("/usr/local/bin/streamtonowhered stop");
}
}%


ss mgtStopOnLinkDown
{
	state init {
		when() {
			pvGet(linkX_up);
			linkX_up0 = linkX_up;
			port = macValueGet("port"); 
		} state wait_link_change
	} 
	
	state wait_link_change {
		when (aggX && linkX_up != linkX_up0 && linkX_up == 0){
			printf("link%s DOWN, stop streaming\n", port);
		} state stop_stream
				
		when(linkX_up != linkX_up0){
			linkX_up0 = linkX_up;
		} state wait_link_change	
	}
	
	state stop_stream {
		when() {
			_stop_stream();
		} state wait_link_up		
	}
	
	state wait_link_up {
		when (((aggX && linkX_up) || (!aggX && !linkX_up))){
		      printf("link%s status OK, resume monitor\n", port);
		      linkX_up0 = linkX_up;		      
		} state wait_link_change
	}
}